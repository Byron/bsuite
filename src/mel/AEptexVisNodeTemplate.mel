
global proc AEptexVisNodeBrowser(string $fileAttribute)
{
	// We use the most primitve file browser just to be maya 2008 compatible ... 
	string $ptxFilter = "*.ptx";
	string $result = `fileDialog -directoryMask $ptxFilter -mode 0`;
	if ($result != "") {
		setAttr $fileAttribute -type "string" $result;
	}
}

global proc AEptexVisNodeNameReplace(string $fileAttribute)
{
	connectControl -fileName ptexVisNodetextureNameField $fileAttribute;
	button -e -c ("AEptexVisNodeBrowser \""+$fileAttribute+"\"") browser;
}

global proc AEptexNameNew(string $fileAttribute)
{
	setUITemplate -pst attributeEditorTemplate;
	rowLayout -nc 3 textureNameLayout;
	{
		text -label "Ptex File";
		textField ptexVisNodetextureNameField;
		symbolButton -image "navButtonBrowse.xpm" browser;
	}
	setParent ..;
	setUITemplate -ppt;
	
	AEptexVisNodeNameReplace($fileAttribute);
}

global proc AEptexVisNodeTemplate(string $nodeName)
{
	editorTemplate -beginScrollLayout;

	editorTemplate -beginLayout "Ptex Texture Attributes" -collapse 0;
	{
		editorTemplate -callCustom  "AEptexNameNew" 
									"AEptexVisNodeNameReplace" 
									"ptexFilePath";
						
		editorTemplate -addControl "ptexFilterType";
		editorTemplate -addControl "ptexFilterSize";
		editorTemplate -ann "texel: Show textures faces in one line; faceRelative: samples along u and v; faceAbsolute: samples along actual positions"
						-addControl "displayMode";
		editorTemplate -addControl "glPointSize";
		editorTemplate -ann "Multiply the amounts of samples taken. Duplicating the value produces 4 times more samples"
					-addControl "sampleMultiplier";
	}
	editorTemplate -endLayout;
	
	editorTemplate -beginLayout "Ptex Texture Information" -collapse 0;
	{
		editorTemplate -ann "StringArray of available metadata keys - only visible via script"
					-l "Meta Data Keys" -addControl "outMetaDataKeys";
		editorTemplate -ann "Amount of channels, e.g. 3 for channels like RGB"
					-l "Channels" -addControl "outNumChannels" ;
		editorTemplate -ann "The ID of the alpha channel or -1 if there is none"
					-l "Alpha Channel" -addControl "outAlphaChannel" ;
		editorTemplate -ann "The amount of mapped faces"
					-l "Faces" -addControl "outNumFaces" ;
		editorTemplate -ann "Amount of texture samples displayed in the viewport" 
					-l "Samples" -addControl "outNumSamples" ;
		editorTemplate -ann "True if edits are (still) available in the ptex file"
					-l "Has Edits" -addControl "outHasEdits" ;
		editorTemplate -ann "True if mipmaps are available" 
					-l "Has MipMaps" -addControl "outHasMipMaps" ;
		editorTemplate -ann "MeshFormat the samples are stored in"
					-l "Mesh Type" -addControl "outMeshType" ;
		editorTemplate -ann "The data type of all channels"
					-l "Data Type" -addControl "outDataType" ;
		editorTemplate -l "U Border Mode" -addControl "outUBorderMode";
		editorTemplate -l "V Border Mode" -addControl "outVBorderMode";
		
	}
	editorTemplate -endLayout;
	
	editorTemplate -addExtraControls;
	editorTemplate -endScrollLayout;
}

